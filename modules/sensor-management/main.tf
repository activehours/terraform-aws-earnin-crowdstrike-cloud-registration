data "aws_caller_identity" "current" {}
data "aws_region" "current" {}
data "aws_partition" "current" {}

locals {
  account_id       = data.aws_caller_identity.current.account_id
  aws_region       = data.aws_region.current.id
  aws_partition    = data.aws_partition.current.partition
  is_gov_account   = var.account_type == "gov"
  lambda_s3_bucket = local.is_gov_account ? "cs-horizon-sensormgmt-lambda-${local.aws_region}" : module.region_map[0].lambda_s3_bucket
  lambda_s3_key    = local.is_gov_account ? "aws/horizon-sensor-installation-orchestrator.zip" : "aws/lambda/horizon-sensor-installation-orchestrator.zip"
}

module "region_map" {
  count      = local.is_gov_account ? 0 : 1
  source     = "../region-map/"
  aws_region = local.aws_region
}


# Data resource to be used as the assume role policy below.
data "aws_iam_policy_document" "management" {
  statement {
    effect  = "Allow"
    actions = ["sts:AssumeRole"]
    principals {
      type        = "AWS"
      identifiers = [var.intermediate_role_arn]
    }
    condition {
      test     = "StringEquals"
      values   = [var.external_id]
      variable = "sts:ExternalId"
    }
  }
}

resource "aws_iam_role" "management" {
  name                 = "${var.resource_prefix}SensorManagement${var.resource_suffix}"
  assume_role_policy   = data.aws_iam_policy_document.management.json
  permissions_boundary = var.permissions_boundary != "" ? "arn:${local.aws_partition}:iam::${local.account_id}:policy/${var.permissions_boundary}" : null
  tags                 = var.tags
}

resource "aws_iam_role_policy" "invoke_lambda" {
  name = "sensor-management-invoke-orchestrator-lambda"
  role = aws_iam_role.management.id
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = [
          "lambda:InvokeFunction",
          "lambda:InvokeAsync"
        ]
        Effect   = "Allow"
        Resource = "arn:${local.aws_partition}:lambda:*:${local.account_id}:function:${var.resource_prefix}cs-*"
        Sid      = "InvokeLambda"
      },
      {
        Action = [
          "ssm:GetDocument",
          "ssm:GetCommandInvocation",
          "ssm:ListCommands",
          "ssm:ListCommandInvocations"
        ]
        Effect   = "Allow"
        Resource = "*"
      }
    ]
  })
}

data "aws_iam_policy_document" "orchestrator" {
  statement {
    effect  = "Allow"
    actions = ["sts:AssumeRole"]
    principals {
      type        = "Service"
      identifiers = ["lambda.amazonaws.com"]
    }
  }
}

resource "aws_iam_role" "orchestrator" {
  name                 = "${var.resource_prefix}SensorManagementOrchestrator${var.resource_suffix}"
  assume_role_policy   = data.aws_iam_policy_document.orchestrator.json
  permissions_boundary = var.permissions_boundary != "" ? "arn:${local.aws_partition}:iam::${local.account_id}:policy/${var.permissions_boundary}" : null
  tags                 = var.tags
}

resource "aws_iam_role_policy" "orchestrator" {
  name = "sensor-management-orchestrator-lambda-ssm-send-command"
  role = aws_iam_role.orchestrator.id
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = [
          "ssm:SendCommand"
        ]
        Effect = "Allow"
        Resource = [
          "arn:${local.aws_partition}:ssm:*:*:document/*",
          "arn:${local.aws_partition}:ec2:*:*:instance/*"
        ]
      },
      {
        Action = [
          "logs:PutLogEvents",
          "logs:CreateLogStream"
        ]
        Effect = "Allow"
        Resource = [
          "arn:${local.aws_partition}:logs:*:*:log-group:/aws/lambda/${var.resource_prefix}cs-*",
          "arn:${local.aws_partition}:logs:*:*:log-group:/aws/lambda/${var.resource_prefix}cs-*:log-stream:*"
        ]
        Sid = "Logging"
      },
      {
        Action = [
          "secretsmanager:GetSecretValue"
        ]
        Effect   = "Allow"
        Resource = "arn:${local.aws_partition}:secretsmanager:${local.aws_region}:${local.account_id}:secret:/CrowdStrike/CSPM/SensorManagement/FalconAPICredentials-??????"
        Sid      = "GetFalconCredentials"
      }
    ]
  })
}

resource "aws_cloudwatch_log_group" "crowdstrike_sensor_management" {
  name              = "/aws/lambda/${var.resource_prefix}cs-horizon-sensor-installation-orchestrator${var.resource_suffix}"
  retention_in_days = 1
}

resource "aws_secretsmanager_secret" "this" {
  name                    = "/CrowdStrike/CSPM/SensorManagement/FalconAPICredentials"
  description             = "Falcon API credentials. Used by the 1-Click sensor management orchestrator"
  recovery_window_in_days = 0

  # EARNIN FORK: optional cross-region read replicas. The secret still lives in
  # the primary region; replicas satisfy DR/multi-region guardrails (e.g. an SCP
  # that denies CreateSecret for single-region secrets). Defaults to none to
  # preserve upstream behavior.
  dynamic "replica" {
    for_each = toset(var.secret_replica_regions)
    content {
      region = replica.value
    }
  }
}

# EARNIN FORK: gated by var.manage_secret_value so the secret value can be
# populated manually instead of by Terraform. Setting it false avoids storing
# the Falcon client secret in Terraform state, which a hard-mandatory TFE
# Sentinel policy blocks. Defaults to true to preserve upstream behavior.
resource "aws_secretsmanager_secret_version" "this" {
  count     = var.manage_secret_value ? 1 : 0
  secret_id = aws_secretsmanager_secret.this.id
  secret_string = jsonencode({
    ClientSecret = var.falcon_client_secret
  })
}

resource "aws_lambda_function" "this" {
  function_name = "${var.resource_prefix}cs-horizon-sensor-installation-orchestrator${var.resource_suffix}"
  role          = aws_iam_role.orchestrator.arn
  handler       = "bootstrap"
  runtime       = "provided.al2023"
  architectures = ["x86_64"]
  memory_size   = 128
  timeout       = 900
  package_type  = "Zip"

  s3_bucket = local.lambda_s3_bucket
  s3_key    = local.lambda_s3_key

  environment {
    variables = merge(
      {
        CS_CLIENT_ID                  = var.falcon_client_id
        CS_API_CREDENTIALS_AWS_SECRET = aws_secretsmanager_secret.this.name
        CS_MODE                       = "force_auth"
      },
      var.is_gov ? {
        CS_GOV_CLOUD = "true"
        CS_ADDRESS   = var.cs_address
      } : {}
    )
  }

  depends_on = [aws_cloudwatch_log_group.crowdstrike_sensor_management]
}
